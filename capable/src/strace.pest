file= _{ SOI ~ (syscall_call | exit)* ~ EOI }
syscall_call = { pid? ~ syscall ~ "(" ~ arguments+ ~ ")" ~ "=" ~ return_code ~ NEWLINE? | signal ~ NEWLINE? }
arguments = _{ argument ~ ("," ~ arguments)? }
syscall = { (CASED_LETTER | ASCII_DIGIT)+ }
argument = _{ array | string_quote | structure | constant | unfinished | comment }
string_list = _{ (argument ~ comment?) ~ ("," ~ string_list)? }
string_quote = _{"\"" ~ string? ~ "\"" ~ "."*}
string = { (( "\\\"" | !"\"" ) ~ ANY)+ } 
array = {"[" ~ (!"]" ~ string_list) ~ "]"}
constant = { ('a'..'z'|'A'..'Z'|"_"|'0'..'9'|"|"|"-")+ }
comment = { "/*"  ~ (!"*/" ~ ANY)+ ~ "*/" }
return_code = { return_value ~ constant? ~ message? }
return_value = { "-"? ~ ASCII_DIGIT+ }
message = { "(" ~ (( "\\(" | !")" ) ~ ANY)+ ~ ")" }
structure = { "{" ~ dict ~ "}" }
dict = _{ key_value ~ ("," ~ (dict | "."+))? }
key_value = _{key ~ "=" ~ value ~ comment? }
key = @{constant}
pid = @{ASCII_DIGIT+}
value = {constant ~ parameters? | structure | array}
parameters = { "(" ~ arguments ~ ")" }
exit = {pid? ~ "+"+ ~ "exited with " ~ return_code ~ "+"+ ~ NEWLINE* | "+"+ ~ "killed by " ~ signal_name ~ "+"+ ~ NEWLINE* }
signal = { pid? ~ "-"+ ~ signal_name ~ structure ~ "-"+ }
signal_name = { "SIG" ~ (CASED_LETTER | ASCII_DIGIT)+ }
unfinished = { "<unfinished ...>" ~ (!"resumed>"  ~ any_or_unfinished)+ ~ "resumed>"}
any_or_unfinished = _{ unfinished | ANY }

NOT_ESCAPE_QUOTE = _{ !"\\" ~ ("\""|"'") }
WHITESPACE       = _{ ((!NOT_ESCAPE_QUOTE) ~ SEPARATOR)+}